/*-----------------------------------------------------------------------------------------------------------------------------------------------
Compañia: 		Eficacia S.A
Proceso:		 Transformacion Cobertura
Desarrollador:  María de los Ángeles Delgado
Email:          maria_adelgado@eficacia.com
Fecha           2023/08/10 (YYYY/MM/DD)
Estado:         Activo
-----------------------------------------------------------------------------------------------------------------------------------------------*/
Rutero_Tmp1:
	LOAD 
		ID_PUNTO_VENTA  									                    as IdPuntoVenta,
		DATE(left(date(FECHA_DE_EJECUCION,'YYYY-MM-DD'),10),'YYYY/MM/DD')	    as Fecha,
		ID_PERSONAL		    								                    as IdPersonal
	FROM 
	[lib://COMP_Stage (eficacia_admin_analitica)/SGI/Colombia/SEVILLA_SA/Rutero_$(Anno)$(Mes).qvd]
	(qvd)	
     Where 
	    ESTADO <> 0
	and ID_PERSONAL <> 168874;

/*---------*/
Visitas_Tmp1:
	LOAD 
		Capitalize(EFECTIVIDAD)													as Efectividad,
		ID_PV															        as IdPuntoVenta, 
		DATE(left(date(FECHA_INICIAL_FILTRO,'YYYY-MM-DD'),10),'YYYY/MM/DD')  	as Fecha,
		ID_ASESOR																as IdPersonal,
		Capitalize(ESTADO)														as Estado,
        ApplyMap('Map_RolAsesor',ID_ASESOR, Null())                             as Rol
	FROM
	[lib://COMP_Stage (eficacia_admin_analitica)/SGI/Colombia/SEVILLA_SA/EfectividadVisitas_$(Anno)$(Mes)*.qvd]
	(qvd)
     Where  ID_ASESOR <> 168874;

/********************************************************************************/
/*							Comienzo Transformacion								*/	
/********************************************************************************/

NoConcatenate
/* +++ Cumplimiento de Rutero +++ */
CumpleVisita_Tmp1:
	LOAD
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		'Cumple Visita' 							as Concepto					
	Resident Rutero_Tmp1;	

Inner Join 
	LOAD
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		'Cumple Visita' 							as Concepto
	Resident Visitas_Tmp1;

/*---------*/
NoConcatenate
/* +++ Visita No Programada(Extra-Ruta) +++ */
Rutero_Tmp:
	LOAD
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.KeyRutero					
	Resident Rutero_Tmp1;

NoConcatenate
ExtraRuta_Tmp1:
	LOAD
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		'Extra Ruta' 								as Concepto
	Resident Visitas_Tmp1
	Where 
		Rol <> 'SUPERVISOR' and
		Not Exists(Ck.KeyRutero,IdPuntoVenta&IdPersonal&Fecha);
	DROP Table Rutero_Tmp;

/*---------*/
NoConcatenate
/* +++ Proximo a Visitar +++ */
PlaneadosVisitar_Tmp:
	LOAD
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		'Visitas Planeadas' 						as Concepto	
	Resident Rutero_Tmp1
	Where 
		Fecha >= Date(Today(),'YYYY/MM/DD');

/*---------*/
NoConcatenate
/* +++ Visitas Incumplidas a hoy +++ */
RuteroVencido_Tmp1:
	LOAD
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha 
	Resident Rutero_Tmp1
	Where 
		Fecha < Date(Today(),'YYYY/MM/DD');
	DROP Table Rutero_Tmp1;

NoConcatenate
Visitas_Tmp:
	LOAD
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.Visitas
	Resident Visitas_Tmp1
	Where Fecha >= '2023/08/01';

NoConcatenate
RuteroVencido_Tmp2:
	LOAD
		Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		'Visitas No ejecutadas' 					as Concepto
	Resident RuteroVencido_Tmp1
	Where
		Fecha >= '2023/08/01' and 
		Not Exists(Ck.Visitas,Ck.Key);
	DROP Table RuteroVencido_Tmp1;
	DROP Table Visitas_Tmp;

/*---------*/
NoConcatenate
/* +++ Cobertura +++ */
Cobertura_Tmp1:
	LOAD
		Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		Concepto
	Resident CumpleVisita_Tmp1;
	DROP Table CumpleVisita_Tmp1;

Concatenate
	LOAD
		Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		Concepto
	Resident ExtraRuta_Tmp1;
	DROP Table ExtraRuta_Tmp1;	

Concatenate 
	LOAD
		Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		Concepto
	Resident PlaneadosVisitar_Tmp;
	DROP Table PlaneadosVisitar_Tmp;		
   
Concatenate
	LOAD
		Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		Concepto
	Resident RuteroVencido_Tmp2;
	DROP Table RuteroVencido_Tmp2;

/*---------*/
NoConcatenate
Cobertura_Tmp2:
	LOAD
		Ck.Key,
		IdPuntoVenta,
		IdPersonal,
		Fecha,
		Concepto
	Resident Cobertura_Tmp1;
	DROP Table Cobertura_Tmp1;

Left Join(Cobertura_Tmp2)
	LOAD 
		IdPuntoVenta&IdPersonal&Fecha 				as Ck.Key,
		Efectividad, 
		Estado
	Resident Visitas_Tmp1;
	DROP Table Visitas_Tmp1;

NoConcatenate
Cobertura:
	LOAD Distinct
		IdPuntoVenta&'-'&IdPersonal&'-'&Fecha	as CK.Key1,
		Fecha							     	as Cobertura.Fecha,
		Ck.Key									as Cobertura.Registros,
		IdPuntoVenta							as Cobertura.IdPuntoVenta,
		IdPersonal								as Cobertura.IdPersona,
		Efectividad								as Cobertura.Efectividad, 
		Estado									as Cobertura.Estado,
		Concepto								as Cobertura.Concepto
	Resident Cobertura_Tmp2;
	DROP Table Cobertura_Tmp2;

STORE Cobertura INTO [lib://COMP_DataMart (eficacia_admin_analitica)/SGI/Colombia/SEVILLA_SA/_Cobertura_$(Anno)$(Mes).qvd];
Drop Table Cobertura;